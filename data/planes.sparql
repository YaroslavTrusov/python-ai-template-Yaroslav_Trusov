# Extracting aircraft with the aircraft engine to see how the engine type affects the maximum altitude, price, and total number of aircraft produced
# Извлекаем самолеты с указанием двигателя самолета, чтобы посмотреть, как влияет тип двигателя на максимальную высоту, цену и общее количество произведенных самолетов
SELECT
  ?aircraft ?aircraftLabel
  ?engine ?engineLabel
  ?maxAltitude
  ?price
  ?totalProduced
WHERE {
  VALUES ?aircraftType { wd:Q15056993 wd:Q11436 }  # Семейство самолётов или модель самолёта

  ?aircraft wdt:P31/wdt:P279* ?aircraftType;# Экземпляр или подкласс "семейства" или "модели"
            wdt:P516 ?engine;               # Двигатель (powered by)
            wdt:P1092 ?totalProduced.       # Количество произведённых самолётов (total produced)

  OPTIONAL { ?aircraft wdt:P2254 ?maxAltitude. } # Максимальная высота (опционально)
  OPTIONAL { ?aircraft wdt:P2284 ?price. }       # Стоимость (опционально)

  SERVICE wikibase:label {bd:serviceParam wikibase:language "ru,en"}
}
ORDER BY DESC(?totalProduced)
